plugins {
    id 'net.freudasoft.gradle-cmake-plugin' version '0.0.4'
}

apply plugin: 'java'
apply plugin: 'application'

group = 'jp.ksgwr'
archivesBaseName = 'kkkvs'
version = '0.0.1'


wrapper {
  gradleVersion = '5.6'
}

mainClassName = 'jp.ksgwr.kkkvs.KKKVS'

repositories {
  mavenCentral()
}

dependencies {
  testCompile('junit:junit:4.12')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


import org.gradle.internal.os.OperatingSystem
test {
    systemProperty "java.library.path", OperatingSystem.current().isWindows() ?
            file("${buildDir}/cmake/Release/").absolutePath
            : file("${buildDir}/cmake/").absolutePath
}

cmake {
    workingFolder = file("$buildDir/cmake")
    sourceFolder = file("$projectDir/src/native")
    platform = OperatingSystem.current().isWindows() ? 'x64' : null
    buildSharedLibs = true
    buildConfig = 'Release'

    if (project.hasProperty("staticLibPath")) {
        def.STATIC_LIB_PATH = project.getProperty("staticLibPath")
    }
    if (project.hasProperty("includeDirPath")) {
        def.INCLUDE_DIR_PATH = project.getProperty("includeDirPath")
    }
}



assemble.dependsOn cmakeBuild
test.dependsOn cmakeBuild
cmakeBuild.dependsOn cmakeConfigure
clean.dependsOn cmakeClean
